apply plugin: "java"
apply plugin: "cpp"

wrapper {
    gradleVersion = "4.8"
}

task generateRT(type: Exec) {
    executable "jlink"
    args "--add-modules"
    args "java.base"
    args "--output"
    args "rt"
}

model {
    repositories {
        libs (PrebuiltLibraries) {
            jvm {
                headers.srcDirs "$JAVA_HOME/include", "$JAVA_HOME/include/linux", "$JAVA_HOME/include/win32"
            }
        }
    }

    components {
        nativeExe (NativeExecutableSpec) {
            sources {
                cpp {
                    lib library: "jvm", linkage: "api"
                    
                    source {
                        srcDirs "src/main/cpp"
                        include "**/*.cpp"
                    }
                }
            }

            binaries.all {
                if (toolChain instanceof Gcc || toolChain instanceof Clang) {
                    cppCompiler.args << "-std=c++14"
                } else if (toolChain instanceof VisualCpp) {
                    cppCompiler.args << "/std:c++14"
                }

                if (targetPlatform.operatingSystem.linux) {
                    linker.args << "-ldl"
                }
            }
        }
    }
}